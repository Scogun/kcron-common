plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.6.21'
    id 'maven-publish'
    id 'signing'
}

group = 'com.ucasoft.kcron'
version = '0.2.2'

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx/" }
}
kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
    }
    linuxX64()
    mingwX64()
    macosX64()
    js()
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.3.2'
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
    }
}

task stubSources(type: Jar) {
    classifier = 'sources'
}

task stubJavadoc(type: Jar) {
    classifier = 'javadoc'
}

task sourceJar(type: Jar){
    classifier = 'sources'
    from kotlin.sourceSets.commonMain.kotlin
}

publishing {
    publications.all {
        def libraryType = it.name
        switch (libraryType) {
            case 'js':
            case 'metadata':
                it.artifactId = "${project.name}-${libraryType}"
                break
            case 'jvm':
                it.artifactId = "${project.name}"
                break
            case 'kotlinMultiplatform':
                it.artifactId = "${project.name}-native"
                break
        }
        pom {
            name = 'KCron Common'
            description = 'Cron realization for Kotlin Multiplatform'
            url = 'https://github.com/Scogun/kcron-common'
            licenses {
                license {
                    name = 'GPL-3.0 License'
                    url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                }
            }
            developers {
                developer {
                    id = 'Scogun'
                    name = 'Sergey Antonov'
                    email = 'SAntonov@ucasoft.com'
                }
            }
            scm {
                connection = 'scm:git:git://github.com/Scogun/kcron-common.git'
                developerConnection = 'scm:git:ssh://github.com:Scogun/kcron-common.git'
                url = 'https://github.com/Scogun/kcron-common'
            }
        }
        if (libraryType != 'kotlinMultiplatform') {
            artifact stubJavadoc
        }
    }
    repositories {
        maven {
            name = 'MavenCentral'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('MAVEN_USERNAME')
                password = System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications
}